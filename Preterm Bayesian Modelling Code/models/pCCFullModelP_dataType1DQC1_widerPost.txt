model{  
      #niso - number of countries
      for(c in 1:niso){
      # data
      
	#n.c is the number of observations within the countries
	for (i in 1:n.c[c]){

	#y is the logit of LBW rate
      	y.ci[c,i] ~ dnorm(mu.ct[c,gett.ci[c,i]]-bias[c,i]-dataTypeB[c,i], nu.ci[c,i])
     
	
	#se.ci is the standard error from the data - survey given, admin estimated by delta method.
      	nu.ci[c,i] <- pow((se.ci[c,i]^2+sigma.y[c,i]^2+ MGA.y[c, i]^2), -1)

	#Additional SD
      	sigma.y[c, i]<-ifelse(source.ci[c,i]==1, sigma1, #Admin - DQC A
		       ifelse(source.ci[c,i]==2, sigma2, #Admin	- DQC B
		       ifelse(source.ci[c,i]==3, sigma3, #Admin	- DQC C
		       ifelse(source.ci[c,i]==4, sigma4, #Nation study - DQC D
                       ifelse(source.ci[c,i]==5, sigma5, 
		       ifelse(source.ci[c,i]==6, sigma6, source.ci[c,i]))))))#Sub-national study - DQC E

	#Adding extra SD for method of GA
	MGA.y[c, i]<-ifelse(MGA[c,i]==1, sigmaM1, 
		     ifelse(MGA[c,i]==2, sigmaM2, 
		     ifelse(MGA[c,i]==3, sigmaM3, MGA[c,i])))

	#Adding singleton/multiple bias
	bias[c,i]<-ifelse(singMult[c,i]==0, bias1,
		   ifelse(singMult[c,i]==1, bias2, singMult[c,i]))

	#Adding dataType bias for studies
	dataTypeB[c,i]<-ifelse((source.ci[c,i]==5 || source.ci[c,i]==4|| source.ci[c,i]==6) && dataType[c,i]==2, tBias1, 
			    ifelse((source.ci[c,i]==5 || source.ci[c,i]==4|| source.ci[c,i]==6) && dataType[c,i]==3, tBias2, dataType[c,i]))

      } #end of n.c
              

        # mean value
	# nyears.c is number of years to estimate for each country - 21.
        for(t in 1:nyears.c[c]){
		
		#Sampling from LBW estimate
		tau.rand[c,t]<-1/(covar_array[2,c,t]^2)
		randCov[c,t]~dnorm(covar_array[1,c,t], tau.rand[c,t])

		cov[1,c,t]<- randCov[c,t] * alpha[1]
		
 	
 	#Option: write out separately covariates to make sure its working
	#mu.ct[c,t]: average logit LBW rate for country c at time t.
	#Spline coefficients:
	#Z.tk: Cubic spline term: H=3, time x H
	#delta: H x number of countries
	#sum(cov[,c,t]): summing all of the covariate*coefficients for country c at time t.

        mu.ct[c,t] <- beta.d[c] + inprod(Z.tk[t,], delta.hc[,c]) + sum(cov[,c,t])
        } # end t
        

          for (h in 1:H){
            delta.hc[h,c] ~ dnorm(0, tau.delta)
          }
  
          beta.d[c] ~ dnorm(mu.beta[region.c[c]], tau.beta[region.c[c]])
	
        } #end iso

	#Non-informative prior on country-specific intercept
	tau.delta~ dunif(0,100) #dunif(0,3)

      	#Non-informative priors on covariate coefficients
      	for(n in 1:ncov){
      		#alpha_tilde[n] ~ dnorm(0,1)   # covariates
      		#alpha[n] <- alpha_tilde[n] *5
		alpha[n]~dnorm(0, tau.alpha)
		 
      	}
	
        tau.alpha<-1/250 # was 1/25

	#Non-informative priors for regional intercept
        for(r in 1:nregions){
          mu.beta[r] ~ dnorm(mu.beta.global, tau.beta.global)
          tau.beta[r] <- pow(sigma.beta[r], -2)
          sigma.beta[r] ~ dunif(0, 40)
        }
        mu.beta.global ~ dnorm(0, 0.0001) #dnorm(0, 0.01)
        tau.beta.global <- pow(sigma.beta.global, -2)
        sigma.beta.global ~ dunif(0, 100) #40
  
	#Data source priors - informative
	sigma1=0.037
	sigma2=sigma1*2
	sigma3=0.2429
	sigma4=sigma3
	sigma5=0.43756
	sigma6=sigma5*2

	#Additional SD for MGA - uniformative priors
	sigmaM1<-0
	sigmaM2<-0.012
	sigmaM3<-0.064

	#Bias for singleton/multiple
	bias1<-0.02015278
	bias2<-0

	#Bias for dataType
	tBias1<-0
	tBias2<-0.05562626

} # end model 
