model{  
      #niso - number of countries
      for(c in 1:niso){
      # data
      
	#n.c is the number of observations within the countries
	for (i in 1:n.c[c]){

	#y is the logit of LBW rate
      	y.ci[c,i] ~ dnorm(mu.ct[c,gett.ci[c,i]]-bias_dt[c,i], nu.ci[c,i])
     
	#se.ci is the standard error from the data - survey given admin estimated by delta method.
	#sigma.y is additional term defined by the rank and the source.
      	nu.ci[c,i] <- pow((se.ci[c,i]^2+sigma.y[c,i]^2), -1)

	#Crude survey bias based on data quality hierarchy of associated admin data
      	bias_dt[c, i]<-ifelse(newFlag[c,i]==1 && source.ci[c,i]==0, bias1,
		     ifelse((newFlag[c,i]==2||newFlag[c,i]==3) && source.ci[c,i]==0, bias2,
		     ifelse((newFlag[c,i]==4||newFlag[c,i]==5) && source.ci[c,i]==0, bias3, 0))) 

        #Addition standard deviation for adjusted survey
      	sigma.y[c, i]<-ifelse(source.ci[c,i]==1, sigma1,

 	#Additional standard deviation for crude Survey - all the same 
	#Change this to be just one equation.
			ifelse(source.ci[c,i]==0, sigma2S, source.ci[c,i]))

      } #end of n.c
              

        # mean value
	# nyears.c is number of years to estimate for each country - 21.
        for(t in 1:nyears.c[c]){
		
		#ncov: Number of covariates - 5
		for(n in 1:ncov){
		cov[n,c,t]<-covar_array[n,c,t] * alpha[n]
		}
 	
	#mu.ct[c,t]: average logit LBW rate for country c at time t.
	#Spline coefficients:
		#Z.tk: Cubic spline term: H=3, time x H
		#delta: H x number of countries
	#sum(cov[,c,t]): summing all of the covariate*coefficients for country c at time t.

        mu.ct[c,t] <- beta.d[c] + inprod(Z.tk[t,], delta.hc[,c]) + sum(cov[,c,t])
        } # end t
        

          for (h in 1:H){
            delta.hc[h,c] ~ dnorm(0, tau.delta)
          }
  
          beta.d[c] ~ dnorm(mu.beta[region.c[c]], tau.beta[region.c[c]])
	
        } #end iso

	#Non-informative prior on country-specific intercept
	tau.delta~ dunif(0,3)

      	#Non-informative priors on covariate coefficients
      	for(n in 1:ncov){
      		#alpha_tilde[n] ~ dnorm(0,1)   # covariates
      		#alpha[n] <- alpha_tilde[n] *5
		alpha[n]~dnorm(0, tau.alpha)
		 
      	}
	#*5 if there were were they needed wider, is that needed?
        tau.alpha<-1/25
	#tau.alpha<-1	

	#Non-informative priors for regional intercept
        for(r in 1:nregions){
          mu.beta[r] ~ dnorm(mu.beta.global, tau.beta.global)
          tau.beta[r] <- pow(sigma.beta[r], -2)
          sigma.beta[r] ~ dunif(0, 40)
        }
        mu.beta.global ~ dnorm(0, 0.01)
        tau.beta.global <- pow(sigma.beta.global, -2)
        sigma.beta.global ~ dunif(0, 40)

	#Informative priors for bias
	bias1<-0
	bias2~dunif(0,40)
        bias3~dunif(0,40)

	#Non-informative priors for data-quality hierarchy
	sigma1<-0
	sigma2S<-0
	

 

} # end model 
